0x52

high

# Fake order can be used to burn tokens from contract via BvbProtocol::reclaimContract

## Summary

BvbProtocol::reclaimContract allows a bull to claim the collateral and premium of an expired order. The issue is that it never checks that an order is actually valid before sending ERC20 tokens. A malicious user could create a fake order and call reclaimContract with it. This order would cause the contract to send order.premium + order.collateral to address(0), permanently losing funds from the contract.

## Vulnerability Detail

    function reclaimContract(Order calldata order) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bull = bulls[contractId];

        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");

        uint bullAssetAmount = order.premium + order.collateral;
        if (bullAssetAmount > 0) {
            // Transfer payment tokens to the Bull
            IERC20(order.asset).safeTransfer(bull, bullAssetAmount);
        }

        reclaimedContracts[contractId] = true;

        emit ReclaimedContract(orderHash, order);
    }

BvbProtocol::reclaimContract allows the user to input any arbitrary order data. The function attempts to validate the order by making sure it's expired and not settled or reclaimed. This validation is insufficient and can be used by a malicious user to send tokens in the contract to address(0). All a malicious users needs to do is construct an order that has a valid expiry. The order won't have ever been in the system so bulls[] will return address(0) and settledContracts[] and reclaimed[] will both return false. The result is that order.premium + order.collateral will be sent to address(0) effectively burning it. Some ERC20 tokens don't allows sending to address(0) but many, notably including WETH allow transfers to address(0).

## Impact

All ERC20 tokens that allow address(0) transfers can be burned from the contract

## Code Snippet

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L417-L443

## Tool used

Manual Review

## Recommendation

I recommend checking that the bull != address(0):

    +   // Check that the bull address is valid
    +   require(bull != address(0), "INVALID_BULL");

        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");
