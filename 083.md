dipp

medium

# Insufficient fee-on-transfer/deflationary token support

## Summary

Due to insufficient support for fee-on-transfer tokens, the ```settleContract``` function could become uncallable if not enough tokens are present in the contract or, if the contract has enough tokens, the contract could suffer a loss of funds since the amount sent to the bear is more than what the contract actually received when ```matchOrder``` was called.

## Vulnerability Detail

In the ```matchOrder``` function in ```BvbProtocol.sol```, when assets are transferred from the maker and taker to the contract there is no check to determine the actual amount received by the contract. If fee-on-transfer tokens are used then the actual amount received by the contract is less than the amount specified in the transfer. When the ```settleContract``` or ```reclaimContract``` functions are called, the amounts to transfer could be more than the amount held in the contract leading to the functions being uncallable.

## Impact

The contract might lose funds such as the fees paid during ```matchOrder```. Also, users might be unable to call ```settleContract``` and ```reclaimContract``` if the contract does not contain enough tokens.

## Code Snippet

[BvbProtocol.sol:matchOrder#L306-L367](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L306-L367):
```solidity
    function matchOrder(Order calldata order, bytes calldata signature) public payable nonReentrant returns (uint) {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        // Check that this order is valid
        checkIsValidOrder(order, orderHash, signature);

        // Fees
        uint bullFees;
        uint bearFees;
        if (fee > 0) {
            bullFees = (order.collateral * fee) / 1000;
            bearFees = (order.premium * fee) / 1000;

            withdrawableFees[order.asset] += bullFees + bearFees;
        }

        address bull;
        address bear;
        uint makerPrice;
        uint takerPrice;

        if (order.isBull) {
            bull = order.maker;
            bear = msg.sender;

            makerPrice = order.collateral + bullFees;
            takerPrice = order.premium + bearFees;
        } else {
            bull = msg.sender;
            bear = order.maker;

            makerPrice = order.premium + bearFees;
            takerPrice = order.collateral + bullFees;
        }

        bulls[contractId] = bull;
        bears[contractId] = bear;

        // Retrieve Taker payment
        if (msg.value > 0) {
            require(msg.value == takerPrice, "INVALID_ETH_VALUE");
            require(order.asset == weth, "INCOMPATIBLE_ASSET_ETH_VALUE");

            WETH(weth).deposit{value: msg.value}();
        } else if(takerPrice > 0) {
            IERC20(order.asset).safeTransferFrom(msg.sender, address(this), takerPrice);
        }
        // Retrieve Maker payment
        if (makerPrice > 0) {
            IERC20(order.asset).safeTransferFrom(order.maker, address(this), makerPrice);
        }

        // Store the order
        matchedOrders[contractId] = order;

        emit MatchedOrder(orderHash, bull, bear, order);

        return contractId;
    }
```

[BvbProtocol.sol:settleContract#L374-L411](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L374-L411):
```solidity
    function settleContract(Order calldata order, uint tokenId) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bear = bears[contractId];

        // Check that only the bear can settle the contract
        require(msg.sender == bear, "ONLY_BEAR");

        // Check that the contract is not expired
        require(block.timestamp < order.expiry, "EXPIRED_CONTRACT");

        // Check that the contract is not already settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        address bull = bulls[contractId];

        // Try to transfer the NFT to the bull (needed in case of a malicious bull that block transfers)
        try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}
        catch (bytes memory) {
            // Transfer NFT to BvbProtocol
            IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
            // Store that the bull has to retrieve it
            withdrawableCollectionTokenId[order.collection][tokenId] = bull;
        }

        uint bearAssetAmount = order.premium + order.collateral;
        if (bearAssetAmount > 0) {
            // Transfer payment tokens to the Bear
            IERC20(order.asset).safeTransfer(bear, bearAssetAmount);
        }

        settledContracts[contractId] = true;

        emit SettledContract(orderHash, tokenId, order);
    }
```

[BvbProtocol.sol:reclaimContract#L417-L443](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L417-L443):
```solidity
    function reclaimContract(Order calldata order) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bull = bulls[contractId];

        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");

        uint bullAssetAmount = order.premium + order.collateral;
        if (bullAssetAmount > 0) {
            // Transfer payment tokens to the Bull
            IERC20(order.asset).safeTransfer(bull, bullAssetAmount);
        }

        reclaimedContracts[contractId] = true;

        emit ReclaimedContract(orderHash, order);
    }
```

## Tool used

Manual Review

## Recommendation

Check the balance of the contract before and after receiving tokens and use the difference in the balance to determine if the user has sent enough tokens.
