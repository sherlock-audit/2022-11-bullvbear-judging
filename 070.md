Tomo

medium

# [Tomo-M1] Can be overwritten the value of bulls and bears

## Summary

Can be overwritten the value of bulls and bears

## Vulnerability Detail

The value of bears, bulls, and matchOrders stores as a mapping with contractId.

This contractId decide by this function

```solidity
function hashOrder(Order memory order) public view returns (bytes32) {
        bytes32 orderHash = keccak256(
            abi.encode(
                ORDER_TYPE_HASH,
                order.premium,
                order.collateral,
                order.validity,
                order.expiry,
                order.nonce,
                order.fee,
                order.maker,
                order.asset,
                order.collection,
                order.isBull
            )
        );

        return _hashTypedDataV4(orderHash);
    }
```

Also, the `checkIsValidOrder` function checks that the nonce is valid, but this checking allows `order.nonce` to be the same value as `minimumValidNonce`. And, no function to add nonce after this checking.

If this is implemented on the front end, the auditor can only determine the vulnerability from the information on the contract so, owner should leave a comment

```solidity
require(order.nonce >= minimumValidNonce[order.maker], "INVALID_NONCE");
```

Thus, the following situations are possible

### Example

`order.maker` = Alice

1. Alice creates two orders same parameters
2. First, Alice matched with Bob. They are paid `takerPrice` and `makerPrice`
3. Next, Alice matched with Carol. They are paid `takerPrice` and `makerPrice` and the matching Alice with Bob is overwritten.
4. Finally, Alice and Bob lost the funds of `takerPrice` and `makerPrice`.

## Impact

 The matching will be overwritten leading to a loss of funds.

## Code Snippet

[https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L728-L761](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L728-L761)

```solidity
/**
 * @notice Checks if an order is valid
 * @param order The order/contract
 * @param orderHash The EIP712 hash of the order
 * @param signature The signature of the order hashed
 */
function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
    // Check that the signature is valid
    require(isValidSignature(order.maker, orderHash, signature), "INVALID_SIGNATURE");

    // Check that this order is still valid
    require(order.validity > block.timestamp, "EXPIRED_VALIDITY_TIME");

    // Check that this order was not canceled
    require(!canceledOrders[orderHash], "ORDER_CANCELED");

    // Check that the nonce is valid
    require(order.nonce >= minimumValidNonce[order.maker], "INVALID_NONCE");
    
    // Check that this contract will expire in the future
    require(order.expiry > order.validity, "INVALID_EXPIRY_TIME");

    // Check that fees match
    require(order.fee >= fee, "INVALID_FEE");

    // Check that this is an approved ERC20 token
    require(allowedAsset[order.asset], "INVALID_ASSET");

    // Check that this if an approved ERC721 collection
    require(allowedCollection[order.collection], "INVALID_COLLECTION");

    // Check that there is no bull set for this order -> not matched
    require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED");
}
```

[https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L763-L812](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L763-L812)

```solidity
/**
 * @notice Checks if a sell order is valid
 * @param sellOrder The sell order of the position
 * @param sellOrderHash The EIP712 hash of the sell order
 * @param order The order/contract
 * @param orderHash The EIP712 hash of the order
 * @param signature The signature of the sell order hashed
 */
function checkIsValidSellOrder(SellOrder calldata sellOrder, bytes32 sellOrderHash, Order memory order, bytes32 orderHash, bytes calldata signature) public view {
    // ContractId
    uint contractId = uint(orderHash);
    
    // Check that the signature is valid
    require(isValidSignature(sellOrder.maker, sellOrderHash, signature), "INVALID_SIGNATURE");

    if (sellOrder.isBull) {
        // Check that the maker is the Bull
        require(sellOrder.maker == bulls[contractId], "MAKER_NOT_BULL");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");
    } else {
        // Check that the maker is the Bear
        require(sellOrder.maker == bears[contractId], "MAKER_NOT_BEAR");

        // Check that the contract hasn't expired
        require(block.timestamp < order.expiry, "CONTRACT_EXPIRED");
    }

    // Check that there is no maker set for this sell order -> not bought
    require(boughtSellOrders[sellOrderHash].maker == address(0), "SELL_ORDER_ALREADY_BOUGHT");

    // Check that this order was not canceled
    require(!canceledSellOrders[sellOrderHash], "SELL_ORDER_CANCELED");

    // Check that this sell order has started
    require(block.timestamp >= sellOrder.start, "INVALID_START_TIME");

    // Check that the sell order hasn't expired
    require(block.timestamp <= sellOrder.end, "SELL_ORDER_EXPIRED");

    // Check that the contract is not settled
    require(!settledContracts[contractId], "SETTLED_CONTRACT");
    
    // Check that the nonce is valid
    require(sellOrder.nonce >= minimumValidNonceSell[sellOrder.maker], "INVALID_NONCE");

    // Check that this is an approved ERC20 token
    require(allowedAsset[sellOrder.asset], "INVALID_ASSET");
}
```

## Tool used

Manual Review

## Recommendation

1. You should change as follows and add a nonce for `order.maker` when creating the market

```solidity
// before
function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
        /* ... */
        // Check that the nonce is valid
        require(order.nonce >= minimumValidNonce[order.maker], "INVALID_NONCE");
        /* ... */
    }

// after
function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
        /* ... */
        // Check that the nonce is valid
        require(order.nonce > minimumValidNonce[order.maker], "INVALID_NONCE");
        /* ... */
    }
```