GimelSec

high

# Malicious Bulls can use `transferPosition()` to bypass `checkIsValidOrder()`.

## Summary

A malicious bull can use `transferPosition()` to set bulls[contractId] to address(0), then `checkIsValidOrder()` can be bypassed. The matched order can be matched again.

## Vulnerability Detail

When an order is matched, it would fill `bulls[contractId]`, `bears[contractId]` and ` matchedOrders[contractId]`.

```solidity
    function matchOrder(Order calldata order, bytes calldata signature) public payable nonReentrant returns (uint) {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        // Check that this order is valid
        checkIsValidOrder(order, orderHash, signature);

       …

        bulls[contractId] = bull;
        bears[contractId] = bear;

       …

        // Store the order
        matchedOrders[contractId] = order;

        emit MatchedOrder(orderHash, bull, bear, order);

        return contractId;
    }
```

Then, the matched order cannot be matched again. `checkIsValidOrder()` checks 
`bulls[uint(orderHash)] == address(0)`

```solidity
    function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
       …

        // Check that there is no bull set for this order -> not matched
        require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED");
    }
```

However, a malicious bull can call transferPosition() to erase `bulls[contractId]`(set `bulls[contractId]` to address(0)).

```solidity
    function transferPosition(bytes32 orderHash, bool isBull, address recipient) public {
        // ContractId
        uint contractId = uint(orderHash);

        if (isBull) {
            // Check that the msg.sender is the Bull
            require(msg.sender == bulls[contractId], "SENDER_NOT_BULL");

            bulls[contractId] = recipient;
        } else {
            // Check that the msg.sender is the Bear
            require(msg.sender == bears[contractId], "SENDER_NOT_BEAR");

            bears[contractId] = recipient;
        }

        emit TransferedPosition(orderHash, isBull, recipient);
    }
```

Therefore, the matched order can be matched again.

## Impact

If a matched order is matched again, the bull and the bear will transfer the premium, the collateral and the fee to the protocol again. So if the order maker is the bear and he/she approves enough allowance to the protocol, a malicious bull can match the order over and over again, leading to a serious loss of funds.

## Code Snippet

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L525-L529

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L760

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L313

## Tool used

Manual Review

## Recommendation

check `matchedOrders` instead of `bulls` in `checkIsValidOrder`

```diff
    function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
       …

        // Check that there is no bull set for this order -> not matched
-       require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED");
+       require(matchedOrders[uint(orderHash)].maker == address(0), "ORDER_ALREADY_MATCHED");
    }
```
