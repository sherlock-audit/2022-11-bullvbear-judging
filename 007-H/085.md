kirk-baird

high

# `reclaimContract()` Can Be Called On Non-Existent Orders

## Summary

The function `reclaimContract()` can be called with an `Order` than was never created. This would then transfer the premium and collateral of the fake order from the `BvbProtocol` contract to `bull[contractId] = address(0)`.

## Vulnerability Detail

There is no check in `reclaimOder()` to ensure that the `order` was first matched.

When `reclaimOrder()` is called with an `order` which was never matched (i.e. `matchOrder()` was not called for that `order`), the following will occur.

- `bulls[contractId] = address(0)` 
- `settledContracts[contractId] = false` 
- `reclaimedContracts[contractId] = false`
- `order.expiry` is any arbitrary value set by the attacker

Therefore, all the `require()` checks will succeed and the ERC20 transfer in the following code snippet occurs.

```solidity
        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");

        uint bullAssetAmount = order.premium + order.collateral;
        if (bullAssetAmount > 0) {
            // Transfer payment tokens to the Bull
            IERC20(order.asset).safeTransfer(bull, bullAssetAmount);
        }
```
## Impact

The impact is that `IERC20(order.asset).safeTransfer(bull, bullAssetAmount)` is called with each of `order.asset` and `bullAssetAmount` being attacker controlled parameters and `bull = address(0)`.

Therefore any tokens stored in the contract can be transferred from the contract to the zero address for any ERC20 token. Since this contract acts as an escrow there will be a significant balance of ERC20 tokens in the contract.

Note: some ERC20 contracts prevent transferring to the zero address and will revert a `transfer()` if the `to` field is `address(0)`. However, most tokens including WETH will successfully transfer funds to the zero address.

## Code Snippet

[reclaimContract()](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L417-L443)
```solidity
    function reclaimContract(Order calldata order) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bull = bulls[contractId];

        // Check that the contract is expired
        require(block.timestamp > order.expiry, "NOT_EXPIRED_CONTRACT");

        // Check that the contract is not settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        // Check that the contract is not reclaimed
        require(!reclaimedContracts[contractId], "RECLAIMED_CONTRACT");

        uint bullAssetAmount = order.premium + order.collateral;
        if (bullAssetAmount > 0) {
            // Transfer payment tokens to the Bull
            IERC20(order.asset).safeTransfer(bull, bullAssetAmount);
        }

        reclaimedContracts[contractId] = true;

        emit ReclaimedContract(orderHash, order);
    }
```

## Tool used

Manual Review

## Recommendation

The `order` must be confirmed to exist before the `reclaimContract()` can be called. There are a few options to do this.
- Check `bulls[contractId] != address(0)`
- Change `reclaimContract(uint256 contractId)` to use the ID and fetched required parameters from `matchedOrders[contractId]`
- Check some field in `matchedOrders[contractId]` is non-zero.
