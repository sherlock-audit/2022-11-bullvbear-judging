0x52

high

# Bull can repeatedly match an order by transfering their position to address(0)

## Summary

Besides the standard checks (i.e. valid collection, not expired, not cancelled etc) the main check that the contract uses to ensure an order hasn't been filled is to check that the bull for the contractID == address(0). A malicious bull can bypass that by using transferPosition to transfer the position to address(0). The bull is now address(0) which allows the contract to be called again. Taking more funds from the bear. 

## Vulnerability Detail

    function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
        // Check that the signature is valid
        require(isValidSignature(order.maker, orderHash, signature), "INVALID_SIGNATURE");

        // Check that this order is still valid
        require(order.validity > block.timestamp, "EXPIRED_VALIDITY_TIME");

        // Check that this order was not canceled
        require(!canceledOrders[orderHash], "ORDER_CANCELED");

        // Check that the nonce is valid
        require(order.nonce >= minimumValidNonce[order.maker], "INVALID_NONCE");
        
        // Check that this contract will expire in the future
        require(order.expiry > order.validity, "INVALID_EXPIRY_TIME");

        // Check that fees match
        require(order.fee >= fee, "INVALID_FEE");

        // Check that this is an approved ERC20 token
        require(allowedAsset[order.asset], "INVALID_ASSET");

        // Check that this if an approved ERC721 collection
        require(allowedCollection[order.collection], "INVALID_COLLECTION");

        // Check that there is no bull set for this order -> not matched
        require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED");
    }

When matching an order it checks that the various details of the order are correct. It checks if the order has been filled previously by checking that the bull hasn't been set. 

    function transferPosition(bytes32 orderHash, bool isBull, address recipient) public {
        // ContractId
        uint contractId = uint(orderHash);

        if (isBull) {
            // Check that the msg.sender is the Bull
            require(msg.sender == bulls[contractId], "SENDER_NOT_BULL");

            bulls[contractId] = recipient;
        } else {
            // Check that the msg.sender is the Bear
            require(msg.sender == bears[contractId], "SENDER_NOT_BEAR");

            bears[contractId] = recipient;
        }

        emit TransferedPosition(orderHash, isBull, recipient);
    }

BvbProtocol::trasnferPosition allows the bull to send their position to address(0). The order will now pass the final validity check and the order can be used again. Each time the order is called it transfers more funds from the bear, allowing the bull to maliciously send more tokens to the BvbProtocol. Excess funds sent to the contract are irretrievable.

## Impact

Bear's order can be used repeatedly, draining their funds.

## Code Snippet

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L734-L761

## Tool used

Manual Review

## Recommendation

Don't allow bull or bear to send position to address(0):

    function transferPosition(bytes32 orderHash, bool isBull, address recipient) public {
        // ContractId
        uint contractId = uint(orderHash);

        if (isBull) {
            // Check that the msg.sender is the Bull
            require(msg.sender == bulls[contractId], "SENDER_NOT_BULL");
    +       require(recipient != address(0), "INVALID RECIPIENT");

            bulls[contractId] = recipient;
        } else {
            // Check that the msg.sender is the Bear
            require(msg.sender == bears[contractId], "SENDER_NOT_BEAR");
    +       require(recipient != address(0), "INVALID RECIPIENT");

            bears[contractId] = recipient;
        }

        emit TransferedPosition(orderHash, isBull, recipient);
    }