0xadrii

high

# NFTs could remain locked forever if the Bull is a malicious contract

## Summary
NFTs will keep locked forever in the contract if a malicious Bull contract does not accept NFTs.
## Vulnerability Detail
When an order is settled, the NFT is transferred to the bull in order to fulfill his Bull position. From the BullvBear docs: "In case the Bull cannot accept NFTs (eg a malicious smart contract), the NFT is kept in our smart contract and can be later retrieved by the Bull through a call to function withdrawToken". 
Currently, the NFT recipient in the `withdrawToken` function is the address set in the mapping `withdrawableCollectionTokenId`. The problem with this solution is that the mapping is updated on detection of the malicious contract (inside the `settleContract` function) and set to be the same malicious Bull contract address. Because of this, in case the `withdrawToken` function is triggered in order to transfer the NFT to the address set in the mapping, it will still fail because the receiver will still be the malicious contract. Also, there's no way to update the `withdrawableCollectionTokenId` in order to change the receiver (it is only done inside `settleContract` on detection of the malicious Bull), so the NFT will remain locked in the smart contract forever, only accessible by the malicious contract which won't be able to receive it.

## Impact
Raising as High because assets would keep locked forever and nobody could access them, as well as the easiness of the attack.
## Code Snippet
https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L399
https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L453-L456
```solidity
function settleContract(Order calldata order, uint tokenId) public nonReentrant {
        ...
        // Try to transfer the NFT to the bull (needed in case of a malicious bull that block transfers)
        try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}
        catch (bytes memory) {
            // Transfer NFT to BvbProtocol
            IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
            // Store that the bull has to retrieve it
            withdrawableCollectionTokenId[order.collection][tokenId] = bull; //@audit the NFT receiver is still set to the malicious bull contract
        }
        ...
}
function withdrawToken(bytes32 orderHash, uint tokenId) public {
        address collection = matchedOrders[uint(orderHash)].collection;

        address recipient = withdrawableCollectionTokenId[collection][tokenId];

        // Transfer NFT to recipient
        IERC721(collection).safeTransferFrom(address(this), recipient, tokenId); //@audit this will fail

        // This token is not withdrawable anymore
        withdrawableCollectionTokenId[collection][tokenId] = address(0);

        emit WithdrawnToken(orderHash, tokenId, recipient);
    }
```
## Tool used

Manual Review

## Recommendation
Make the `withdrawToken` function only accessible to the Bull (which is the legitimate NFT receiver) by checking the order data, and add a parameter to the function to allow a new recipient. For example:
```solidity
function withdrawToken(bytes32 orderHash, uint tokenId, address newRecipient) public {
        address collection = matchedOrders[uint(orderHash)].collection;

        address recipient = withdrawableCollectionTokenId[collection][tokenId];

        require(msg.sender == recipient, "Only bull is allowed to withdraw");

        // This token is not withdrawable anymore
        withdrawableCollectionTokenId[collection][tokenId] = address(0);

        // Transfer NFT to recipient
        IERC721(collection).safeTransferFrom(address(this), newRecipient, tokenId); 
        emit WithdrawnToken(orderHash, tokenId, recipient);
    }
```