hansfriese

high

# The NFT might be locked inside the protocol forever after the contract was settled.

## Summary
The NFT might be locked inside the protocol forever after the contract was settled.

## Vulnerability Detail
In `settleContract()`, when the `Bear` tries to settle the contract, it stores the NFT inside the `BvbProtocol` if it fails to transfer to the `Bull`.

```solidity
    // Try to transfer the NFT to the bull (needed in case of a malicious bull that block transfers)
    try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}
    catch (bytes memory) {
        // Transfer NFT to BvbProtocol
        IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
        // Store that the bull has to retrieve it
        withdrawableCollectionTokenId[order.collection][tokenId] = bull;
    }
```

And it updates the mapping `withdrawableCollectionTokenId()` so that the `Bull` can retrieve the NFT later.

As we can see from [this comment](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L393), we consider it might fail to transfer the NFT to the `Bull` by a malicious `Bull` because he would like to prevent the contract being settled.

But it might be possible that the `Bull` is an honest user but he doesn't notice his contract can't receive NFTs by default.

In this case, the `Bull` should retrieve the NFT using `withdrawToken()` after the settlement.

```solidity
    function withdrawToken(bytes32 orderHash, uint tokenId) public {
        address collection = matchedOrders[uint(orderHash)].collection;

        address recipient = withdrawableCollectionTokenId[collection][tokenId];

        // Transfer NFT to recipient
        IERC721(collection).safeTransferFrom(address(this), recipient, tokenId);

        // This token is not withdrawable anymore
        withdrawableCollectionTokenId[collection][tokenId] = address(0);

        emit WithdrawnToken(orderHash, tokenId, recipient);
    }
```

But there is no option to change the `recipient` to receive the NFT and it always tries to transfer the NFT to the stored `Bull` address and it will always revert.

- Bob created a bearish order and Alice matched the order.
- So the order was matched between `Alice(Bull)` and `Bob(Bear)`.
- Alice is using a contract that can't receive NFTs for some reason but she forgot to use other wallets or contracts.
- Bob settled the contract and the NFT was transferred to the protocol because Alice can't receive the NFT.
- The contract was settled properly and Alice tries to withdraw the NFT using `withdrawToken()`.
- In `withdrawToken()`, it always tries to transfer the NFT to Alice's contract and it will always revert.
- Alice wants to receive the NFT using another wallet but there is no such option and the NFT will be locked inside the protocol forever.

## Impact
When the `Bull`'s contract can't receive NFTs maliciously or by fault, the NFT will be locked inside the protocol contract forever after the contract settlement.

## Code Snippet
https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L394-L400
https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L449-L462

## Tool used
Manual Review

## Recommendation
I think we should modify `withdrawToken()` to withdraw the NFT using the custom `to` address like below.

```solidity
    function withdrawToken(bytes32 orderHash, uint tokenId, address to) public { //++++++++++++++++
        address collection = matchedOrders[uint(orderHash)].collection;

        address recipient = withdrawableCollectionTokenId[collection][tokenId];

        require(msg.sender == recipient, "Invalid caller");

        // Transfer NFT to recipient
        IERC721(collection).safeTransferFrom(address(this), to, tokenId); //++++++++++++++++++

        // This token is not withdrawable anymore
        withdrawableCollectionTokenId[collection][tokenId] = address(0);

        emit WithdrawnToken(orderHash, tokenId, to); //+++++++++++++++++
    }
```